plugins {
  id("idea")
  id("java")

  id("com.google.protobuf")   version "0.9.5"
  id("com.diffplug.spotless") version "7.2.1"
  id("io.gatling.gradle")     version "3.14.3.5"
}

repositories {
  mavenCentral()
}

dependencies {
  gatlingImplementation("io.gatling:gatling-grpc-java:3.14.3.1")
}

gatling {
  enterprise {
    // Enterprise Cloud (https://cloud.gatling.io/) configuration reference: https://docs.gatling.io/reference/integrations/build-tools/gradle-plugin/#running-your-simulations-on-gatling-enterprise-cloud
  }
}

var generatedSources = [
  file("${protobuf.generatedFilesBaseDir}/gatling/java"),
  file("${protobuf.generatedFilesBaseDir}/gatling/grpc")
]

idea {
  module {
    generatedSources.forEach { generatedSourceDirs += it }
  }
}

sourceSets {
  gatling {
    java {
      generatedSources.forEach { srcDirs += it }
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.25.8"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.74.0"
    }
  }
  generateProtoTasks {
    ofSourceSet("gatling").forEach { task ->
      // A plugin somewhere doesn't handle task dependencies correctly on custom source sets
      compileGatlingJava.dependsOn(task)
      task.plugins {
        grpc {}
      }
    }
  }
}

spotless {
  java {
    palantirJavaFormat()
    importOrder("java|javax", "scala", "io.gatling", "", "\\#")
    removeUnusedImports()
    target project.fileTree(project.rootDir) {
      include "**/*.java"
      exclude "build/generated/**/*.*"
    }
    toggleOffOn()
    trimTrailingWhitespace()
  }
}
