plugins {
  id("idea")
  id("java")

  id("com.google.protobuf")   version "0.9.4"
  id("com.diffplug.spotless") version "6.25.0"
  id("io.gatling.gradle")     version "3.10.4"
}

repositories {
  mavenCentral()
}

dependencies {
  gatlingImplementation("io.gatling:gatling-grpc-java:3.10.4.1")
}

gatling {
  enterprise {
    // Enterprise Cloud (https://cloud.gatling.io/) configuration reference: https://gatling.io/docs/gatling/reference/current/extensions/gradle_plugin/#working-with-gatling-enterprise-cloud
    // Enterprise Self-Hosted configuration reference: https://gatling.io/docs/gatling/reference/current/extensions/gradle_plugin/#working-with-gatling-enterprise-self-hosted
  }
}

var generatedSources = [
  file("${protobuf.generatedFilesBaseDir}/gatling/java"),
  file("${protobuf.generatedFilesBaseDir}/gatling/grpc")
]

idea {
  module {
    generatedSources.forEach { generatedSourceDirs += it }
  }
}

sourceSets {
  gatling {
    java {
      generatedSources.forEach { srcDirs += it }
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.25.3"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:1.62.2"
    }
  }
  generateProtoTasks {
    ofSourceSet("gatling").forEach { task ->
      // A plugin somewhere doesn't handle task dependencies correctly on custom source sets
      compileGatlingJava.dependsOn(task)
      task.plugins {
        grpc {}
      }
    }
  }
}

spotless {
  java {
    palantirJavaFormat()
    importOrder("java|javax", "scala", "io.gatling", "", "\\#")
    removeUnusedImports()
    target project.fileTree(project.rootDir) {
      include "**/*.java"
      exclude "build/generated/**/*.*"
    }
    toggleOffOn()
    trimTrailingWhitespace()
  }
}
