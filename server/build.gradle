plugins {
  id 'application'
  id 'idea'
  id 'java'
  id 'com.google.protobuf'   version '0.9.4'
  id 'com.diffplug.spotless' version '6.20.0'
}

repositories {
  mavenCentral()
}

dependencies {
  compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
  implementation 'io.grpc:grpc-netty:1.57.2'
  implementation 'io.grpc:grpc-protobuf:1.57.2'
  implementation 'io.grpc:grpc-services:1.57.2' // reflection
  implementation 'org.bouncycastle:bcpkix-jdk18on:1.76'
}

idea {
  module {
    sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.24.2'
  }
  plugins {
    grpc {
      artifact = 'io.grpc:protoc-gen-grpc-java:1.57.2'
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

spotless {
  java {
    palantirJavaFormat()
    importOrder('java|javax', 'scala', 'io.gatling', '', '\\#')
    removeUnusedImports()
    target project.fileTree(project.rootDir) {
      include '**/*.java'
      exclude 'build/generated/**/*.*'
    }
    trimTrailingWhitespace()
  }
}

ext {
  //javaMainClass = "io.gatling.grpc.demo.server.calculator.CalculatorServer"
  javaMainClass = "io.gatling.grpc.demo.server.greeting.GreetingServer"
}

application {
  mainClassName = javaMainClass
}
